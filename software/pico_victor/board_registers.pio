.pio_version 1
.define CLOCK_15B_PIN 30 
.define CLOCK_5_PIN  29
.define ALE_PIN 23 
.define RD_PIN 20

.define ADDRESS_RD_WR_SIZE 22
.define ADDRESS_BUS_SIZE 20
.define REGISTER_SIZE 12
.define DATA_SIZE 8

.program dma_registers

; --- Pre-amble ---
; This code runs once at the start for each state machine initialization
; It expects a bitmask of the 12 MSBs of the address bus to be used for the DMA Board Registers
pull block          ; Pull bitmask for the 0xEF300 MSB that I'll match against later
mov y, osr          ; Store bitmask in Y register for 12 MSB address comparison later

.wrap_target
T0:
    mov isr, null                      ; clear ISR
    mov pindirs, null                  ; set bus control pins to input
    wait 0 GPIO CLOCK_5_PIN            ; Wait for CLK5 to go low
    
T1:   
    wait 1 GPIO ALE_PIN                ; wait for ALE to go high = T1 start
    wait 0 GPIO CLOCK_15B_PIN          ; Wait for CLK15B to go low to allow for address to stabilize
    in pins, ADDRESS_BUS_SIZE          ; shift 12 bits of address into ISR
    in null, 20                        ; move the address into the lower 12 bits of the register, dropping 8 LSB to compare against 12 MSB
    mov x, isr                         ; X = 12 MSB address bits
    jmp x!=y, T0                       ; if we're not at 0xEF300, restart T0 to try again
    mov isr, null                      ; CRITICAL FIX: Clear ISR before reading address again
    in pins, ADDRESS_BUS_SIZE          ; gather full address to send to arm

T2:
    wait 0 GPIO ALE_PIN                ; wait for ALE to go low = T1 start
    wait 0 GPIO CLOCK_5_PIN            ; Wait for CLK5 to go low //waiting two CLK15 cycles for signal setup time
    wait 1 GPIO CLOCK_5_PIN            ; Wait for CLK5 to go high
    jmp pin T3_WRITE                   ; if RD pin is high 8088 is writing, if low 8088 is reading

T3_READ:                               ; 8088 read cycle // pico's writing
    mov osr, ~null                     ; set OSR to all 1's
    in osr, 12                         ; Shift in 12 bits of 1's (0xFFF) to signal arm we're reading
    push                               ; Push the current ISR address + read signal onto RX FIFO
    pull block                         ; Pull data to send to 8088 from TX FIFO
    out pins, DATA_SIZE                ; drive BD0-BD7 with data for 8088 to read
    out pindirs, DATA_SIZE             ; set BD0-BD7 as outputs
    wait 1 GPIO RD_PIN                ; wait for RD to go high (end of read cycle)
.wrap

T3_WRITE:                              ; 8088 write cycle // pico's reading
    wait 1 GPIO CLOCK_5_PIN            ; Wait for CLK5 to go high
    wait 0 GPIO CLOCK_5_PIN            ; Wait for CLK5 to go low
    in pins, DATA_SIZE                 ; read data the 8088 is writing max 0xFF
    in null, 4                         ; shift in 4 null bits to signal it's a write
    push                               ; send address from earlier and data to arm for storage
    jmp T0                             ; jump to T4 for the next clock cycle

% c-sdk {
#include <stdio.h>
#include "pico/error.h"
#include "pico_victor/dma.h"
#define ADDRESS_RD_WR_SIZE 22 

static inline void dma_registers_program_init(PIO pio, uint sm, uint offset) {
    printf("starting to configure dma_registers_program_init\n");
    pio_sm_config c = dma_registers_program_get_default_config(offset);

    // init all the pins i'll be using as output
    for (int i = 0; i < ADDRESS_RD_WR_SIZE; i++) {
        uint pin = BD0_PIN + i;
        pio_gpio_init(pio, pin);
       // printf("pio_gpio_init %d\n", pin);
    }
   pio_sm_set_consecutive_pindirs(pio, sm, BD0_PIN, ADDRESS_RD_WR_SIZE, false);  

   pio_gpio_init(pio, ALE_PIN);
   pio_gpio_init(pio, CLOCK_5_PIN);
   pio_gpio_init(pio, CLOCK_15B_PIN);
   
   sm_config_set_in_pins(&c, BD0_PIN);
   sm_config_set_in_pin_count(&c, ADDRESS_BUS_SIZE);
   sm_config_set_out_pins(&c, BD0_PIN, DATA_SIZE);
   sm_config_set_jmp_pin(&c, RD_PIN); // RD pin is the pin we check for write vs read cycle
   
   sm_config_set_out_shift(&c, true, false, DATA_SIZE);
   sm_config_set_in_shift(&c, true, false, ADDRESS_BUS_SIZE);
   printf("Config execctrl before init: 0x%08x pinhi:0x%08x pinctrl:0x%08x\n",
          (uint32_t)c.execctrl, (uint32_t)c.pinhi, (uint32_t)c.pinctrl);
   
   sm_config_set_clkdiv(&c, 1.0f);  
   
   pio_sm_clear_fifos(pio, sm);

   // Load our configuration, and jump to the start of the program
   int init_rc = pio_sm_init(pio, sm, offset, &c);
   printf("pio_sm_init rc=%d, SM%d execctrl after init=0x%08x\n", init_rc, sm, pio->sm[sm].execctrl);
   if (init_rc == PICO_OK) {
       pio_sm_set_enabled(pio, sm, true);
   } else {
       printf("WARNING: pio_sm_init failed; SM%d not enabled\n", sm);
   }

   printf("finished configuring dma_registers_program_init\n");
   printf("HOLD_PIN Pin %d func: %d, dir: %d\n", HOLD_PIN, gpio_get_function(HOLD_PIN), gpio_get_dir(HOLD_PIN));
   printf("RD_PIN Pin %d func: %d, dir: %d\n", RD_PIN, gpio_get_function(RD_PIN), gpio_get_dir(RD_PIN));
   printf("WR_PIN Pin %d func: %d, dir: %d\n", WR_PIN, gpio_get_function(WR_PIN), gpio_get_dir(WR_PIN));

   uint32_t execctrl = pio->sm[sm].execctrl;
   uint jmp_pin = (execctrl >> 24) & 0x1F;
   printf("SM%d EXECCTRL=0x%08x, JMP_PIN=%u\n", sm, execctrl, (unsigned)jmp_pin);
}
%}
