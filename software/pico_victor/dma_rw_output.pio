.pio_version 1
.define RD_PIN 21
.define ALE_PIN 24
.define HLDA_PIN 29
.define DATA_SIZE 8

.define FIFO_REG_READ    0x00
.define FIFO_WRITE_VALUE 0x01
.define FIFO_DMA_READ    0x02

.define DMA_REGISTER_BITMASK 0x00000EF3
.define DMA_MASK_SIZE 12
.define ADDRESS_BUS_SIZE 20

.program dma_rw_output

; --- Pre-amble ---
; This code runs once at the start for each state machine initialization
; we need the pindirs for the address (input)/data (output) 0xFFF00 pins to be set for DMA read cycles
pull block                ; Pull pindirs for RD cycle from FIFO
mov y, osr                ; Store pindirs for RD cycle in Y

.wrap_target
DMA_T1:
    wait 1 GPIO HLDA_PIN  ; wait for HLDA to go high
    pull block            ; Pull T1 payload for address phase from FIFO [2 bits W/R][20 bits address A19-A0]
    out pins, 20          ; output address on bus, we're driving A0-A19 for ALE in T1
    mov pindirs, ~null    ; drive address pins output

DMA_T2:
    out x, 2              ; get W/R 2-bit flag from LSB (3=Write, 0=Read)
    wait 1 irq 2          ; wait for DMA read_write SM to signal move from address to data phase
    pull block            ; Pull T2 new payload WR: [A19-A8] [data BD7-BD0] from FIFO for WR T2
                          ;         new payload RD: [A19-A8] [data=0x00 for BD7-BD0] from FIFO for RD T2
    out pins, 20          ; output data byte plus MSB of address on BD0-BD7 and A8-A19
    jmp !x DMA_T2_READ    ; if x=0 (read), jump to DMA_T2_READ
    ; Fall through for write (x=1)

DMA_T2_WRITE:
    mov pindirs, ~null    ; drive [A19-A8] [data BD7-BD0] as outputs for DMA WR
    jmp T4_END            ; wait for cycle complete

DMA_T2_READ:
    mov pindirs, y        ; set [A19-A8] outputs, [BD7-BD0] inputs,  for DMA RD
    wait 1 irq 3          ; wait for DMA read_write SM to signal read capture
    in pins, DATA_SIZE    ; RD case, read data from bus
    in x, 2               ; move the DMA read flag into ISR as MSBs, came from out at start of DMA_T2
    push                  ; push data to FIFO for ARM to read push payload RD: [payload type=3 /2bits][data BD7-BD0 /8bits][address A19-A0 /20bits]
    ; Fall through to T4_END

T4_END:
    wait 1 irq 4          ; wait for source SM dma_rw_control to signal cycle complete
    mov pindirs, null     ; tri-state bus (BD0-BD7 for registers, BD0-A19 for DMA)
.wrap

% c-sdk {
#include <stdio.h>
#include "hardware/pio.h"
#include "hardware/gpio.h"
#include "pico_victor/dma.h"

static inline void dma_rw_output_program_init(PIO pio, uint sm, uint offset) {
    printf("Configuring PIO for dma_rw_output SM\n");
    pio_sm_config c = dma_rw_output_program_get_default_config(offset);

    // Configure OUT pins for BD0-A19 (20 pins starting at BD0_PIN)
    sm_config_set_out_pins(&c, BD0_PIN, 20);

    // Configure IN pins for reading address bus (20 pins starting at BD0_PIN)
    sm_config_set_in_pins(&c, BD0_PIN);

    // Configure JMP pin for EXTIO (to distinguish register reads from register writes)
    //using presence of TX FIFO data to identify DMA pending
    sm_config_set_jmp_pin(&c, EXTIO_PIN);

    sm_config_set_clkdiv(&c, 1.0f);

    // NOW initialize GPIO pins AFTER pio_sm_init
    // Initialize GPIO pins for bus control
    for (int i = BD0_PIN; i < (BD0_PIN + ADDRESS_BUS_SIZE); i++) {
        pio_gpio_init(pio, i);
        gpio_set_drive_strength(i, GPIO_DRIVE_STRENGTH_2MA);
        gpio_set_slew_rate(i, GPIO_SLEW_RATE_SLOW);
        gpio_disable_pulls(i);
        pio_sm_set_pins_with_mask(pio, sm, 0u, 1u << i); // preload low
    }

    // Set all bus pins as inputs initially (will be controlled by pindirs instructions)
    pio_sm_set_consecutive_pindirs(pio, sm, BD0_PIN, ADDRESS_BUS_SIZE, false);

    // Load our configuration, and jump to the start of the program
    int init_rc = pio_sm_init(pio, sm, offset, &c);
    printf("dma_rw_output pio_sm_init rc=%d, SM%d execctrl after init=0x%08x\n", init_rc, sm, pio->sm[sm].execctrl);
    if (init_rc == PICO_OK) {
        pio_sm_set_enabled(pio, sm, true);
    } else {
        printf("WARNING: pio_sm_init failed; SM%d not enabled\n", sm);
    }
}

%}